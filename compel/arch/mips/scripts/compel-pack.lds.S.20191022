OUTPUT_ARCH(mips)
EXTERN(__export_parasite_head_start)

SECTIONS
{
/*		. = 0x120000000 + SIZEOF_HEADERS; */
	.text : {  
		*(.head.text)
		ASSERT(DEFINED(__export_parasite_head_start),	
			"Symbol __export_parasite_head_start is missing");
        	*(.text*)
		*(.compel.exit)
		*(.compel.init)
	}

	.rodata : {
		*(.rodata*)
		*(.got*)
	}

	.data : ALIGN(8) {  /*转换规则：将所有输入文件中的名字为“.data*”、“.bss”的段依次合并到输出文件的".data"*/
		*(.data*)
		*(.bss*)
		*(.sbss*)
	}
	
	.toc : ALIGN(8) {
		*(.toc*)
	}

	/DISCARD/ : { /*释义：需要丢弃的输入文件段*/
		*(.debug*)
		*(.pdr)	
		*(.comment*)
		*(.note*)
		*(.group*)
		*(.eh_frame*)
		*(.MIPS.options)
		*(.gnu.attributes)
	}

/* Parasite args should have 4 bytes align, as we have futex inside. */
. = ALIGN(8);
__export_parasite_args = .;
}
